plugins {
    id 'application'
    id 'org.gradlex.extra-java-module-info' version '1.7'
    id 'com.github.iherasymenko.jlink' version '0.6'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

application {
    mainClass = 'com.example.demo.DemoApplication'
    mainModule = 'demo.main'
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.2.1')
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.mysql:mysql-connector-j'
}

tasks.withType(JavaCompile).configureEach { task ->
    task.options.release = 21
    task.options.compilerArgs += '-parameters'
}

extraJavaModuleInfo {
    module("com.mysql:mysql-connector-j", "mysql.connector.j") {
        closeModule()
        requiresTransitive("java.sql")
        requires("jdk.crypto.ec")
        exports("com.mysql.cj.jdbc")
    }
}